\comment This is the source for the Asymptote FAQ list, in
\comment the Bizarre Format With No Name.  It is turned into Lout
\comment input, HTML, plain ASCII and an Info document by a Perl script.
\comment
\comment The format and scripts come from the Linux FAQ, by
\comment Ian Jackson.
\set brieftitle Asymptote FAQ
\set author     <A href="http://asymptote.sourceforge.net">Asymptote</A> 
\set title      Asymptote Frequently Asked Questions
\copyto ASCII
            ASYMPTOTE FREQUENTLY ASKED QUESTIONS
                            `date '+%d %h %Y'`
			   
\endcopy
\copyto INFO
START-INFO-DIR-ENTRY
* ASYMPTOTE FAQ: (asy-faq). ASY Frequently Asked Questions.
END-INFO-DIR-ENTRY


File: $prefix.info, Node: Top, Next: Question 1.1, Up: (dir)

            ASYMPTOTE FREQUENTLY ASKED QUESTIONS
                            `date '+%d %h %Y'`
			  
\endcopy

This is the list of Frequently Asked Questions about Asymptote (asy).

\section  Index

\index

\comment ######################################################################

\section  Introduction and general information

\question 26jun:whatisasy  What is Asymptote?

Asymptote (asy for short) is a vector graphics language designed for
technical graphics, inspired by MetaPost but with floating-point numerics
and a C++-like syntax. 

\question 22jun:whereisasy  How do I obtain Asymptote?

New releases are available in binaries for several platforms and as source
code from the website \docref{http://asymptote.sourceforge.net/\}.  Many Linux
distributions now contain an Asymptote package (check your distribution's
documentation for further information about this).

\question 28jun:beforeasking Where can I ask questions about Asymptote? 

If you have a question, please try to find an answer in this FAQ, in the 
extensive Asymptote documentation at
\docref{http://asymptote.sourceforge.net/doc/\}, or search the forum:
\docref{http://sourceforge.net/forum/forum.php?forum_id=409349\}.

\comment ######################################################################

\section  Questions about installation and setup

\question 26jun:windir The path "%USERPROFILE%\\.asy\\config.asy" is incomprehensible.

That is the way that Microsoft Windows refers to the user profile directory. 
There's nothing really to understand here, just put your configuration
commands in the file config.asy in a new folder "%USERPROFILE%\\.asy".

\question 26jun:osx Is it possible to install Asymptote on Mac OS X?

Yes. Marius Schamschula maintains a binary package for OS X Tiger (10.4), which
can be found at
\docref{http://www.hmug.org/pub/MacOS_X/BSD/Applications/Publishing/asymptote/\}.
It is also easy to compile Asymptote directly from sources
\docref{http://sourceforge.net/project/showfiles.php?group_id=120000\}.

\question 26jun:osxbadCPU On installing Asymptote from the MAC OS binary I get the error "Bad CPU type in executable".

This either means that you have a binary distribution for another MAC
architecture, or (according to Marius Schamschula) that you may have a missing
library. The simplest solution is to compile Asymptote directly from the
official source:
\docref{http://sourceforge.net/project/showfiles.php?group_id=120000\}.

\question 27jun:latexintegration Is it possible to integrate Asymptote into LaTeX?

Yes, see the example latexusage.tex. Dario Teixeira has also written a detailed
guide on the topic. You can download it from
\docref{http://dario.dse.nl/projects/asylatex/\}.

\question 27jun:winglobal How do I change environment variables in Windows, for example, in order to change the default PostScript viewer?

While it is easier to set the corresponding Asymptote configuration
variable in your config.asy file, here is the procedure for changing
Windows environment variables:
 
Click on the [Start] button 
* RIGHT-click on 'My Computer' 
* Choose 'Properties' from the popup menu 
* Click the 'Advanced' tab 
* Click the 'Environment Variables' button. 

\comment #####################################################################

\section Questions about programming

\question 27jun:genarrays Is there a way to define functions that act on arrays in general (i.e. work for arrays of any type)?

Generic types aren't yet implemented. 
 
But for now you can at least say 
\verbatim
typedef string T; 
include F; 
 
typedef real T; 
include F; 
\endverbatim

where F.asy contains some type-dependent code like
T[] operator $(T A, T B) {return new T[] {A,B};}

\question 27jun:cirdep Circular dependencies: is there any way to declare structures ahead of their definition, e.g. where struct A performs some operation on struct B, but B contains an A member.

Asymptote does not support forward declaration of types. You can, however, 
nest structures, so that both types are visible for parts of the bodies of 
both structure definitions. For example: 
 
\verbatim
struct B { 
typedef void someroutine(B b); 
 
static struct A { 
someroutine routine; 
void init(someroutine routine) { 
this.routine=routine; 
} 
} 
 
A a=new A; 
string test="Testing"; 
} 
 
typedef B.A A; 
 
A operator init() {return new A;} 
B operator init() {return new B;} 
 
 
A a; 
a.init(new void(B b){write(b.test);}); 
 
B b; 
a.routine(b);
\endverbatim

\question 27jun:comporint Is Asymptote an interpreter or a compiler?  

Asymptote compiles Asymptote commands into its own virtual machine code. It 
then runs this pseudocode on a virtual machine to produce postscript code. 

\question 29jun:matlabels Can Asymptote use matrices as labels?

Yes: 
\verbatim
texpreamble("\usepackage{amsmath}"); 
label("$\begin{matrix} 1 & 2 \\\ 1 & 1 \end{matrix}$",(0,0)); 
\endverbatim

\comment #####################################################################

\section Questions about differences between Asymptote and MetaPost

\question 29jun:interp What is the equivalent of the MetaPost c[a,b] interpolation operator? 

Use
\verbatim
interp(a,b,c);
\endverbatim

\question 23jun:mp3dots What is the equivalent of MetaPost ... command?

::

This is just a macro for tension atleast 1:

\verbatim
size(100);
pair z0=(0,0);
pair z1=(1,0.25);
pair z2=(2,0);
draw(z0{up}..tension atleast 1 ..z1{right}..tension atleast 1 ..z2{down});
\endverbatim

\question 23jun:mppickup What is the equivalent of the MetaPost pickup command?

Just say, for example:

\verbatim
currentpen=red;
\endverbatim

\question 29aug:whatever What is the equivalent of the MetaPost whatever command?

* whatever

Asymptote does not implicitly solve linear equations and therefore does not
have the notion of a @code{whatever} unknown. Such a facility could
certainly be added (perhaps using the notation ?= since = means assignment).
However, the most common uses of whatever in MetaPost are covered by
functions like extension in math.asy: 
\verbatim
pair extension(pair P, pair Q, pair p, pair q);
\endverbatim
this returns the intersection point of the extensions of the line segments 
@code{PQ} and @code{pq}. I find using routines like extension more explicit
and less confusing to new users. But we could be persuaded to add something
similar if someone can justify the need. In the meantime, one can always
use the explicit built-in linear solver (based on LU decomposition).

\question 23jun:lray What is the equivalent for the MetaPost command for lray - horiz*v - verti*u = whatever*(LightSource - R), a system of three linear equations for three unknowns: horiz, verti, whatever.

Since horiz*v+verti*u describes a plane, you could use

\verbatim
real intersect(vector P, vector Q, vector n, vector Z);
\endverbatim

to find the intersection time for the line lray-whatever*(LightSource - R);
and then extract the three desired values from there. (You'll still need to
use the built-in explicit linear solver to solve a 2x2 system to get horiz
and verti.)

\question 27jun:unitsizes In MetaPost, it is possible to have a drawing remain the same size in different pictures by defining a unit u and explicitly multiply all the coordinates by u.  Is there a better way to do this in Asymptote?

Yes, Asymptote has a better way: you definitely don't want to manually scale 
all of your coordinates. To make the user coordinates represent multiples
of exactly 1cm:

\verbatim
draw(unitsquare);
shipout(unitsize=1cm);
\endverbatim 

Another way is to draw your fixed size object to a frame and add it to
currentpicture like this:
 
\verbatim
path p=(0,0)--(1,0); 
frame object; 
draw(object,scale(100)*p); 
 
add(object); 
add(object,(0,-10)); 
\endverbatim

To understand the difference between frames and pictures, try this: 
 
\verbatim
size(300,300); 
 
path p=(0,0)--(1,0); 
picture object; 
draw(object,scale(100)*p); 
 
add(object); 
add(object,(0,-10)); // Adds truesize object to currentpicture 
\endverbatim

\question 28jun:tiles In MetaPost, one could produce tiling pictures by generating a picture, and then clipping the picture to a rectangle of fixed dimensions around the centre of the picture. How is that done in Asymptote?

If you are using currentpicture the way one would in MetaPost (drawing in 
raw PostScript coordinates), you can simply do something like: 
 
\verbatim
fill((0,0)--(100,100)--(200,0)--cycle); 
 
pair center(picture pic=currentpicture) {return 0.5*(pic.min()+pic.max());} 
 
real height=100; 
real width=100; 
pair delta=0.5(width,height); 
pair c=center(); 
clip(box(c-delta,c+delta)); 
\endverbatim

However, drawing in PostScript coordinates is often inconvenient. Here's the 
Asymptote way of doing the same thing, using deferred drawing: 
 
\verbatim
size(200,0); 
fill((0,0)--(1,1)--(2,0)--cycle); 
 
void clip(picture pic=currentpicture, real width, real height) 
{
pic.clip(new void (frame f, transform) { 
pair center=0.5(min(f)+max(f)); 
pair delta=0.5(width,height); 
clip(f,box(center-delta,center+delta)); 
}); 
} 
 
clip(100,100); 
\endverbatim

See also the index entry "tilings" in the documentation. 

\comment ######################################################################

\section  Questions about 2D graphs

\question 26jun:vertlbls How can I prevent axes tick labels from rendering on top of each other?

Either:

(i) give LeftTicks/RightTicks/Ticks the arguments beginlabel=false
and/or endlabel=false;

(ii) use the xasy GUI to move overlapping labels;

(iii) change the Label argument of LeftTicks to:

\verbatim
Label(currentpen+overwrite(Move))
\endverbatim

Solution (iii) will move labels that might otherwise overwrite a previous
label. Other possible overwrite arguments are Allow (allows overlapping
labels; the default), Suppress (an overlapping label will not be written at
all), SuppressQuiet, and MoveQuiet. The last two achieve the same result as
the non-quiet types, but will not notify you which labels are overlapping. See: 
\docref{http://asymptote.sourceforge.net/doc/Pens.html\}.
 
In the case of a user-specified tick array, you can change which labels get
suppressed/moved by changing the order of array entries.

\question 26jun:arrows1 The default Asymptote arrow head is a triangle, but the PSTricks and XFig packages have other types of arrowheads. Is the a way to create another type of arrowhead?

Yes, you can build your own arrowheads by modifying the arrowhead routine in 
plain_arrows.asy: 
 
\verbatim
arrowhead=new guide(path g, real position, pen p=currentpen, real size=0,
real angle=arrowangle) 
{ 
... 
} 
\endverbatim

If you submit your alternate arrowheads, we'll consider including them in a
future release.

\question 26jun:custompalettes Is it possible to define customized palettes?

Yes, you may generate your own pen[] array. For example: 

\verbatim
int NColors=32768;
pen[] MyPalette=new pen[NColors]; 
real step=1/(NColors-1.0); 
// Start at black: rgb(0,0,0) 
// End at yellow: rgb(1,1,0) 
for(int i=0; i < NColors; ++i) { 
  real rgval=i*step; 
  MyPalette[i]=rgb(rgval,rgval,0.0); 
} 
\endverbatim

\question 26jun:factorial Is there an easy way to graph factorial functions nicely?

The example below shows a continuous function and two methods for placing 
markers at integer values of x: 
 
\verbatim
import graph; 
 
size(200,200,IgnoreAspect); 
 
real factorial(real t) {return gamma(t+1);} 
 
scale(Linear,Log); 
 
// Graph the factorial function.
draw(graph(factorial,0,10)); 
 
// Method 1: Draw nodes, but hide line
pair F(int t) {return (t,factorial(t));} 
// Graph of factorial function from 0 to 10
pair[] z=sequence(F,11);
draw(graph(z),invisible,marker(scale(0.8mm)*unitcircle,blue,Fill,Below)); 
 
// Method 2: Nongraphing routines require explicit scaling:
pair dotloc(int t) {return Scale(F(t));} 
pair[] dotlocs=sequence(dotloc,11);
dot(dotlocs); 
 
xaxis("$x$",BottomTop,LeftTicks); 
yaxis("$y$",LeftRight,RightTicks); 
\endverbatim

\question 26jun:length How do you indicate that a certain length should be exactly the size you prescribe with no rescaling, within a picture which has its own size?

Here's an easy way to do this.
 
\verbatim
size(12cm,0); 
 
void distance(picture pic=currentpicture, pair A, pair B, Label L="", real n=0,
	      pen p=currentpen) 
{
  real d=3mm;
  guide g=A--B;
  transform T=shift(-n*d*unit(B-A)*I);
  pic.add(new void(frame f, transform t) {
    picture opic;
    guide G=T*t*g;
    draw(opic,G,p,Arrows(NoFill),Bars,PenMargins); 
    label(opic,L,midpoint(G),UnFill(1)); 
    add(f,opic.fit());
  });
  pic.addBox(min(g),max(g),T*min(p),T*max(p));
} 
 
pair A=(0,0), B=(3,3); 
 
dot(A); 
dot(B); 
 
distance(A,B,"$\ell$",1); 
\endverbatim

\question 26jun:log2 I would like to have my y-axis display (base-2) logarithmic values. How do I do that?

See the example log2graph.asy. 
 
\question 26jun:decsep Is there any way to change the default appearance of the decimal separator in axis ticks, allowing for a comma instead of a dot?

Just set your locale appropriately. For example, under the UNIX bash shell:
 
\verbatim
LC_NUMERIC=it_IT asy -V lineargraph 
\endverbatim

\question 27jun:dots Shouldn't dots always be the same size?

From the documentation: 
 
"The dot command defined in the module plain draws a dot having a diameter 
equal to an explicit pen linewidth or the default linewidth magnified by 
dotfactor (6 by default)."
 
Thus, when you use the default pen, the dot will have size 6*linewidth,
but when you give a pen with an explicit width specified, you will 
have a dot of size linewidth. If you want the first case to behave like the
second, you may set dotfactor=1.

\question 27jun:align I'm trying to draw two graphs on the same figure with their x axes aligned. However, the y ticks are not the same length, so one x axis is shrinked with respect to the other. How can align these two graphs?

Here is solution that fits the second picture to a frame based on the
horizontal scaling for the first picture:
\verbatim
import graph;  
 
real width=15cm; 
real aspect=0.3; 
 
picture pic1,pic2; 
 
size(pic1, width, aspect * width, IgnoreAspect); 
size(pic2, width, aspect * width, IgnoreAspect); 
 
real xmin=0; 
real xmax=2*pi; 
 
real a1=1; 
real a2=0.001; 
 
real f1(real x) {return a1*sin(x);} 
real f2(real x) {return a2*sin(x);} 
 
draw(pic1,graph(pic1,f1,xmin,xmax)); 
draw(pic2,graph(pic2,f2,xmin,xmax)); 
 
xaxis(pic1,Bottom,LeftTicks); 
xaxis(pic2,"$x$",Bottom,LeftTicks); 
yaxis(pic1,"$f_1(x)$",Left,RightTicks); 
yaxis(pic2,"$f_2(x)$",Left,RightTicks); 
 
yequals(pic1,0,dotted); 
yequals(pic2,0,dotted); 
 
add(pic1.fit(),(0,0),NW); 
transform t1=pic1.calculateTransform(); 
 
transform t2=pic2.calculateTransform(); 
add(pic2.fit(xscale(t1.xx)*yscale(t2.yy)),(0,0),SW); 
\endverbatim

\question 27jun:changeaxis How can I change the direction of the y-axis, such that negatives values are on the upper y-axis?

Here is a simple example (see also the example diatom.asy or the discussion
of Linear(-1) in the documentation):
 
\verbatim
import graph; 
 
size(250,200,IgnoreAspect); 
 
scale(Linear,Linear(-1)); 
 
real Sin(real t) {return sin(2pi*t);} 
real Cos(real t) {return cos(2pi*t);} 
 
draw(graph(Sin,0,1),red,"$\sin(2\pi x)$"); 
draw(graph(Cos,0,1),blue,"$\cos(2\pi x)$"); 
 
xaxis("$x$",BottomTop,LeftTicks); 
yaxis("$y$",LeftRight,RightTicks); 
 
label("LABEL",point(0),UnFill(1mm)); 
 
attach(legend(),point(E),20E,UnFill); 
\endverbatim

\question 27jun:functioncolor How can I fill a path with a function that defines the color of each location?

General function shading is only supported by PDF, not PostScript, so this 
will have to wait until we produce PDF code directly. 
 
An approximate solution for now would be to superimpose a fine grid and 
specify colors to latticeshade depending on position as a single pen[][]
lattice. Or use Gouraud shading.

\question 27jun:nonexplicitfun Is there a way to draw a function that is not explicitly given, such as (y - 2)^2 = x - 1 ?

Yes, use the parametric form 

\verbatim
y=t 
x=(t-2)^2+1 
\endverbatim

See the example parametricgraph.asy. 

\question 27jun:colorssaturation The binary operator * can be used to scale the color of a pen by a real number. Does this scaling factor have to be less than 1?

The scaling factor can be greater than 1. But keep in mind that the rgb color 
components saturate at 1. 
 
Try  
\verbatim
write(cyan); write(0.8*cyan); write(1.5*cyan); 
\endverbatim
and you will quickly see what is going on. 
 
To get a lighter cyan you can say white+cyan, 
which yields rgb(0.5,1,1). If you want something even lighter specify the 
rgb colors directly, 
for example, rgb(0.9,1,1). 
 
Alternatively, work in cmyk colour space, which is nicer in that it handles 
saturation separately from hue: 
 
0.1*Cyan is light and 0.9*Cyan is dark. 
You can also say things like 0.1*(red+cmyk) 

\question 27jun:scalesecondaryaxis  Is it possible to reverse or stretch an axis?

The real scaling argument to Linear is used to stretch (or reverse) the axis. 
To see the effect of axis stretching, be sure not to specify IgnoreAspect in 
the picture size command.
 
A secondary axis has the same length as the primary axis, so stretching cannot 
have any effect. But one can still reverse the axis, with Linear(-1).

\comment ######################################################################

\section  Questions about output and debugging

\question 27jun:latexfonts How do I change the LaTeX font used for labels? 

Try using  
 
\verbatim
texpreamble("\usepackage{mathptmx}"); 
\endverbatim

\question 27jun:psviewer How can I disable automatic invocation of the PS viewer after an asy file is done processing?

It's actually not on by default, unless you happen to be using  
Windows (because that is what most MSWindows users expect). Windows users 
can turn this feature off with the command-line option -noV or by putting 
 
\verbatim
import settings; 
interactiveView=false; 
batchView=false; 
\endverbatim

in their config.asy file. See
\docref{http://asymptote.sourceforge.net/doc/Options.html\}.

\question 26jun:jpeg How do I output jpeg images?

If you have the ImageMagick convert program installed, you can run the following: 
asy -f jpg test.asy 

\question 26jun:debug Is there a debugger for asy?

Release 1.10 includes a line-based debugger.

\question 26jun:miktex Asymptote freezes upon trying to draw a label with my installation of MikTex in Windows, 

Likely, this means that latex and dvips are not in your default path.
Try adding the following lines to your config.asy file: 

\verbatim
latex="C:\texmf\miktex\bin\latex.exe"; 
dvips="C:\texmf\miktex\bin\dvips.exe"; 
\endverbatim

After that it also asks to install the xcolor package, which you should do. 
Then it should work. 

\question 27jun:picarray Several of my asy files use an array of pictures. What is a convenient way to declare and initialize the array?

You could write yourself a routine such as:
\verbatim
picture[] picture(int n) { 
  picture[] pic; 
  for(int i=0; i < n; ++i) { 
    pic[i]=new picture; 
    size(pic[i],19cm,0); 
  } 
  return pic; 
} 
 
picture[] pic=picture(6); 
\endverbatim

\question 27jun:texpatches Do you accept patches for Asymptote?

Yes, in fact we would prefer that users submit patches for customized features,
instead of relying on us to do all of the coding. Development will proceed 
faster that way. 
 
\question 27jun:embedbitmaps I want to embed large bitmaps (photos) into my drawings, and would like to move them around and scale them.
 
Simply convert them to eps and use label(graphic("file"),(0,0)), etc. 
 
See the example orthocenter.asy and
\docref{http://asymptote.sourceforge.net/doc/label.html\}.

\question 28jun:rus How can I use international fonts in Asymptote labels?

See \docref{http://asymptote.sourceforge.net/doc/unicode.html\}.

\question 10jul:Fourier How can one use Fourier fonts? 

\verbatim
texpreamble("\usepackage{fourier}");
defaultpen(font("T1","fut\textfamilyextension","m","n"));
\endverbatim

\question 28jun:directpdf Does Asymptote support direct PDF output?

Currently, PDF conversion is possible with the -f pdf option,
which supports transparency, annotations, and embedded movies and u3d images.
However, we plan to eventually produce direct PDF output to support
features not available in PostScript (e.g. general function shading).

\question 28jun:bigpictures I can't figure out how to produce large pictures of high quality in raster format (e.g. png, giff etc). 

Try using some of the options to convert, mainly -geometry and -density. For example: 
convert -geometry 1000x3000 example.eps example.png 
should give you the number of pixels you are looking for. 
 
You can also change the default resolution of the image with: 
convert -geometry 1000x3000 -density 300 -units PixelsPerInch example.eps 
example.png 
This does not change the number of pixels in the image, but just gives a hint 
as to how large a pixel should be displayed.
 
If you include the -density option without the -geometry option, convert will 
keep the image size constant (so a 4"x3" eps figure will generate a 4"x3" png 
image). 

\question 28jun:multipage Is it possible to produce multi-page documents with asymptote? 

Yes, simply call the newpage() function.

\comment Here it ends!

